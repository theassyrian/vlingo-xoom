plugins {
    id 'maven'
    id 'java'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}
group = 'io.vlingo'
version = '1.0.0-RC1'

dependencies {
    annotationProcessor "io.micronaut:micronaut-inject-java:$micronautVersion"
    annotationProcessor "io.micronaut:micronaut-validation:$micronautVersion"
    implementation "io.micronaut:micronaut-inject-java:$micronautVersion"
    implementation "io.micronaut:micronaut-validation:$micronautVersion"
    implementation "io.micronaut:micronaut-management:$micronautVersion"
    implementation "com.google.guava:guava:28.1-jre"
    compile "jakarta.activation:jakarta.activation-api:1.2.1"
    compile "com.sun.activation:javax.activation:1.2.0"
    compile "io.micronaut:micronaut-http-client:$micronautVersion"
    implementation "io.micronaut.configuration:micronaut-netflix-ribbon:1.0.4"
    compile 'io.swagger.parser.v3:swagger-parser:2.0.17'
    compile "io.micronaut:micronaut-runtime:$micronautVersion"
    compile "io.vlingo:vlingo-http:$vlingoVersion"
    compile "io.vlingo:vlingo-actors:$vlingoVersion"
    compile "io.vlingo:vlingo-symbio:$vlingoVersion"
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "Mozilla Public License 2.0"
            url "https://mozilla.org/MPL/2.0/"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "kbastani"
            name "Kenny Bastani"
            email "kennybastani@gmail.com"
        }
    }

    scm {
        url "scm:git:ssh://github.com/vlingo/vlingo-xoom.git"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'io.vlingo'
            artifactId 'vlingo-xoom-server'
            version '1.0.0-RC1'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Reactive, scalable, and resilient HTTP servers and RESTful services running on vlingo/cluster and vlingo/actors.')
                root.appendNode('name', 'vlingo-xoom-server')
                root.appendNode('url', 'https://github.com/vlingo/vlingo-xoom')
                root.children().last() + pomConfig
            }
        }
    }
}

task buildAll {
    clean
    assemble
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['MyPublication']

    pkg {
        repo = 'vlingo-xoom'
        name = 'vlingo-xoom'
        userOrg = 'vlingo'
        licenses = ['MPL-2.0']
        vcsUrl = 'https://github.com/vlingo/vlingo-xoom.git'
    }
}
